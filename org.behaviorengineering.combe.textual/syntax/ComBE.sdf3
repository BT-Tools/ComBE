module ComBE

context-free start-symbols
  
    Start

templates

	Start.Specification = 
		<
		#RT <Requirement*; separator="\n">
	  	
		<Component*; separator="\n\n">
		
		<Root?>
		>
 
	Requirement.Requirement = <<INTorID> <Label>>
	Component.StandardComponent = 
		<
		#C <INTorID> <Label> 
			<BehaviorSeq*; separator="\n">
		>
	Component.SystemComponent = 
		<
		#SC <INTorID> <Label>
			<BehaviorSeq*; separator="\n">
			>
	BehaviorSeq.BehaviorSeq = <<BehaviorType> <Behavior*; separator="\n">>
	Behavior.Behavior = <<INTorID> <Label>>
	
	Root.Root = <#T <BehaviorTree>> %% workaround for GMF phantom node bug
	BehaviorTree.AtomicSequence = <<AtomicSequence> <AtomicSequenceChild?>>
	BehaviorTree.AlternativeBranch = 
		<
		<AtomicSequence> #N { 
			<BranchChildren>
		}
		>
	BehaviorTree.ParallelBranch = 
		<
		<AtomicSequence> #P {
			<BranchChildren>
		}
		>
	AtomicSequence = <<Node+; separator=";;">>
	AtomicSequenceChild = <; <BehaviorTree>>
	BranchChildren = <<BehaviorTree*; separator="\n">>

	Node.StandardNode = <<INTorID> <TraceabilityStatus?> <INTorID> <INTorID> <LabelAndOp?>>
	Node.EmptyNode = <_ <LabelAndOp?>>
	LabelAndOp = <<NodeLabel?> <Operator>>
		
 lexical syntax
  	
  	StateRealisation:"#S"	-> BehaviorType
	Selection:"#L"			-> BehaviorType
	Event:"#E"				-> BehaviorType
	Guard:"#G"				-> BehaviorType
	IIEvent:"#II"			-> BehaviorType
	IOEvent:"#IO"			-> BehaviorType
	EIEvent:"#EI"			-> BehaviorType
	EOEvent:"#EO"			-> BehaviorType
	Assertion:"#A"			-> BehaviorType
  	
  	Implied:"+"				-> TraceabilityStatus
	Missing:"-"				-> TraceabilityStatus
	Updated:"++"			-> TraceabilityStatus 
	Deleted:"--"			-> TraceabilityStatus
	Refinement:"+-"			-> TraceabilityStatus
  	
	BranchKill:"--"			-> Operator
	Conjunction:"&"			-> Operator
	Disjunction:"|"			-> Operator
	May:"%"					-> Operator
	Reference:"=>"			-> Operator
	Reversion:"^"			-> Operator
	StartNew:"^^"			-> Operator
	Synchronisation:"="		-> Operator
	XOR:"XOR"				-> Operator
	
  lexical syntax
    
    [a-zA-Z0-9]+         -> INTorID
    ~[\n\r\ \;\#]+	     -> Label
    ~[\n\r\ \;]+	     -> NodeLabel
	
  lexical restrictions
  
    %% Ensure greedy matching for lexicals
    INTorID            -/- [a-zA-Z0-9\_]
    Label         -/- ~[\n\r\ \;\#]